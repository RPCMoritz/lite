plugins {
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'java'
	id 'jvm-test-suite'
	id 'war'
}

group = 'com.structurizr'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
	mavenCentral()
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

tasks.named('check') {
	dependsOn(testing.suites.integrationTest)
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:3.0.4'
	implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:10.1.5'
	implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0'
	implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'

	implementation 'com.structurizr:structurizr-dsl:1.30.2'
	implementation 'com.structurizr:structurizr-graphviz:2.0.1'

	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.13'
	implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.6.21'
	implementation 'org.jruby:jruby-core:9.4.0.0'

	implementation 'org.apache.lucene:lucene-core:8.11.1'
	implementation 'org.apache.lucene:lucene-queryparser:8.11.1'
}

bootWar {
	requiresUnpack '**/kotlin-*.jar'
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}